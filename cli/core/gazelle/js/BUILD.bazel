load("@bazel_gazelle//:def.bzl", "gazelle_binary")
load("@io_bazel_rules_go//go:def.bzl", "go_library", "go_test")
load("//cli/core/gazelle:gazelle.bzl", "gazelle_generation_test")

# Disable the JS extension
# gazelle:js disabled
# gazelle:exclude tests/

go_library(
    name = "js",
    srcs = [
        "config.go",
        "configure.go",
        "fix.go",
        "generate.go",
        "kinds.go",
        "language.go",
        "resolve.go",
        "target.go",
    ],
    importpath = "github.com/aspect-build/silo/cli/core/gazelle/js",
    visibility = ["//visibility:public"],
    deps = [
        "//cli/core/gazelle/common",
        "//cli/core/gazelle/common/git",
        "//cli/core/gazelle/common/log",
        "//cli/core/gazelle/js/node",
        "//cli/core/gazelle/js/parser",
        "//cli/core/gazelle/js/parser/treesitter",
        "//cli/core/gazelle/js/pnpm",
        "//cli/core/gazelle/js/proto",
        "//cli/core/gazelle/js/typescript",
        "@bazel_gazelle//config:go_default_library",
        "@bazel_gazelle//label:go_default_library",
        "@bazel_gazelle//language:go_default_library",
        "@bazel_gazelle//repo:go_default_library",
        "@bazel_gazelle//resolve:go_default_library",
        "@bazel_gazelle//rule:go_default_library",
        "@com_github_bmatcuk_doublestar_v4//:doublestar",
        "@com_github_emirpasic_gods//maps/linkedhashmap",
        "@com_github_emirpasic_gods//maps/treemap",
        "@com_github_emirpasic_gods//sets/treeset",
        "@com_github_emirpasic_gods//utils",
    ],
)

go_test(
    name = "js_test",
    srcs = ["generate_test.go"],
    embed = [":js"],
)

# Internal only for tests
gazelle_binary(
    name = "gazelle_js_binary",
    languages = [
        "@bazel_gazelle//language/proto:go_default_library",
        ":js",
    ],
    visibility = ["//visibility:private"],
)

# A separate generation test for each tests/* test case
[
    gazelle_generation_test(
        name = "%s_test" % test_workspace.replace("/WORKSPACE", "").replace("tests/", ""),
        dir = test_workspace.replace("/WORKSPACE", ""),
        gazelle_binary = "gazelle_js_binary",
    )
    for test_workspace in glob(["tests/**/WORKSPACE"])
]
