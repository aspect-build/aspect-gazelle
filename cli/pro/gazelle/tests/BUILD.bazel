load("//cli/core/gazelle:gazelle.bzl", "gazelle_generation_test")

BUILTIN_PLUGINS_DIR = "cli/pro/gazelle/plugins"

# Disable the gazelle extensionsextensions
# gazelle:js disabled
# gazelle:bzl disabled
# gazelle:kotlin disabled
# gazelle:aspect-configure disabled
# gazelle:exclude .

# kotlin plugin tests
KOTLIN_TESTS = [t.replace("/WORKSPACE", "") for t in glob(["kotlin/*/WORKSPACE"])]

[
    gazelle_generation_test(
        name = "%s_test" % t.replace("kotlin/", "kotlin-"),
        data = [
            "//cli/pro/gazelle/plugins:kotlin.lang.star",
            "//cli/pro/gazelle/plugins:maven.lang.star",
        ],
        dir = t,
        env = {
            "TEST_STARZELLE_PLUGINS": BUILTIN_PLUGINS_DIR,
        },
        gazelle_binary = "//cli/pro/gazelle/host:gazelle_host_binary",
    )
    for t in KOTLIN_TESTS
]

# bzl plugin tests
BZL_TESTS = [t.replace("/WORKSPACE", "") for t in glob(["bzl/*/WORKSPACE"])]

[
    gazelle_generation_test(
        name = "%s_test" % t.replace("bzl/", "bzl-"),
        data = [
            "//cli/pro/gazelle/plugins:bzl.lang.star",
        ],
        dir = t,
        env = {
            "TEST_STARZELLE_PLUGINS": BUILTIN_PLUGINS_DIR,
        },
        gazelle_binary = "//cli/pro/gazelle/host:gazelle_host_binary",
    )
    for t in BZL_TESTS
    # TODO: remove opt-outs
    if t.replace("bzl/", "") not in [
        "bazel_tools",
        "defaultvisibility",
        "empty",
        "external",
        "multidir",
        "tests",
        "relative_import",
    ]
]
