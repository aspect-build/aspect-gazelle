load("@gazelle//:def.bzl", "gazelle_binary")
load("//:gazelle.bzl", "gazelle_generation_test")

BUILTIN_PLUGINS_DIR = "language/orion/plugins"

# Disable the gazelle extensions
# gazelle:ignore

# bzl plugin tests
BZL_TESTS = [t.replace("/WORKSPACE", "") for t in glob(["bzl/*/WORKSPACE"])]

[
    gazelle_generation_test(
        name = "%s_test" % t.replace("bzl/", "bzl-"),
        data = [
            "//%s:bzl.lang.star" % BUILTIN_PLUGINS_DIR,
        ],
        dir = t,
        env = {
            "TEST_STARZELLE_PLUGINS": BUILTIN_PLUGINS_DIR,
        },
        gazelle_binary = "//language/orion/tests:gazelle_orion_binary",
    )
    for t in BZL_TESTS
    # TODO: remove opt-outs
    if t.replace("bzl/", "") not in [
        "bazel_tools",
        "defaultvisibility",
        "empty",
        "external",
        "multidir",
        "tests",
        "relative_import",
    ]
]

# Internal only for tests
gazelle_binary(
    name = "gazelle_orion_binary",
    testonly = True,
    languages = [
        "//language/orion",
    ],
    visibility = ["//language/orion/tests:__subpackages__"],
)
