load("@gazelle//:def.bzl", "gazelle_binary")
load("@rules_go//go:def.bzl", "go_library", "go_test")
load("//:gazelle.bzl", "gazelle_generation_test")

# gazelle:exclude tests/

go_library(
    name = "js",
    srcs = [
        "config.go",
        "configure.go",
        "fix.go",
        "generate.go",
        "kinds.go",
        "language.go",
        "resolve.go",
        "target.go",
    ],
    importpath = "github.com/aspect-build/aspect-gazelle/language/js",
    visibility = ["//visibility:public"],
    deps = [
        "//common",
        "//common/cache",
        "//common/logger",
        "//common/rule",
        "//language/js/node",
        "//language/js/parser",
        "//language/js/pnpm",
        "//language/js/proto",
        "//language/js/typescript",
        "@com_github_bazelbuild_buildtools//build",
        "@com_github_bmatcuk_doublestar_v4//:doublestar",
        "@com_github_emirpasic_gods//maps/linkedhashmap",
        "@com_github_emirpasic_gods//maps/treemap",
        "@com_github_emirpasic_gods//sets/treeset",
        "@com_github_emirpasic_gods//utils",
        "@gazelle//config",
        "@gazelle//label",
        "@gazelle//language",
        "@gazelle//repo",
        "@gazelle//resolve",
        "@gazelle//rule",
    ],
)

go_test(
    name = "js_test",
    srcs = ["generate_test.go"],
    embed = [":js"],
)

# The gazelle binary to run local gazelle tests.
# Internal only for tests.
gazelle_binary(
    name = "gazelle_js_binary",
    testonly = True,
    languages = [
        "@gazelle//language/proto:go_default_library",
        ":js",
    ],
    visibility = ["//visibility:private"],
)

# A separate generation test for each tests/* test case
[
    gazelle_generation_test(
        name = "%s_test" % test_workspace.replace("/WORKSPACE", "").replace("tests/", ""),
        dir = test_workspace.replace("/WORKSPACE", ""),
        gazelle_binary = "gazelle_js_binary",
    )
    for test_workspace in glob(["tests/**/WORKSPACE"])
]
